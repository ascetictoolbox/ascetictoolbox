// Block definition (there may be more than one block per file)
FUNCTION_BLOCK adaptor

// Define input variables
VAR_INPUT
    currentDifference : REAL;
    trendDifference : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    urgency : REAL;
END_VAR

// Fuzzify input variable 'currentDifference'
FUZZIFY currentDifference
    TERM small := (0, 1) (4, 0) ; 
    TERM medium := (1, 0) (4,1) (6,1) (9,0);
    TERM large := (6, 0) (9, 1);
END_FUZZIFY

// Fuzzify input variable 'trendDifference' indicates if the trend is getting worse or not
FUZZIFY trendDifference
    TERM worse := (5,0) (10,1);
    TERM better := (0, 1) (5,0) ;
END_FUZZIFY

// Defzzzify output variable 'urgency'
DEFUZZIFY urgency
    TERM low := (0,0) (5,1) (10,0);
    TERM medium := (10,0) (15,1) (20,0);
    TERM immediate := (20,0) (25,1) (30,0);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF currentDifference IS small OR trendDifference IS better 
                THEN urgency IS low;

    RULE 2 : IF currentDifference IS medium 
                THEN urgency IS medium; 

    RULE 3 : IF currentDifference IS large
                THEN urgency is immediate;
				
	RULE 4 : IF currentDifference IS medium AND trendDifference IS worse 
                THEN urgency is immediate;
				
END_RULEBLOCK

END_FUNCTION_BLOCK